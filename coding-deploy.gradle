//将项目发布到腾讯coding平台的相关task，coding平台可以作为一个Maven仓库，省去了自建maven仓库的麻烦，也不再需要自己购买服务器
Properties properties = new Properties()
properties.load(rootProject.file("local.properties").newDataInputStream())

def repoUrl = properties.get("repoUrl")
project.group = 'com.xxc.dev'
def artifactId = project.name
project.version = project.android.defaultConfig.versionName
System.err.println("implementation >> ${group}:${artifactId}:${version}")

apply plugin: 'maven'

def userName = System.getenv("CODING_ACCOUNT")
def password = System.getenv("CODING_PASSWORD")
//System.err.println(repoUrl)
System.err.println("Coding username is :$userName , password is: $password")

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        excludes = ['**/*.kt']  // < ---- Exclude all kotlin files from javadoc file.
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${repoUrl}") {
                authentication(
                        userName: "${userName}",
                        password: "${password}"
                )
            }
            pom.version = "$project.version"
            pom.artifactId = "$artifactId"
            pom.groupId = "$project.group"
        }
    }
}